(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*             Xavier Leroy, projet Cristal, INRIA Rocquencourt           *)
(*                                                                        *)
(*   Copyright 1996 Institut National de Recherche en Informatique et     *)
(*     en Automatique.                                                    *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

[@@@ocaml.warning "-27-32"]

(* Environment handling *)

open Cmi_format
open Config
open Misc
open Asttypes
open Longident
open Path
open Types
open Btype

let state = Local_store.new_bindings ()
let sref f = Local_store.ref state f
let srefk k = Local_store.ref state (fun () -> k)

let add_delayed_check_forward = ref (fun _ -> assert false)

let value_declarations : ((string * Location.t), (unit -> unit)) Hashtbl.t ref =
  sref (fun () -> Hashtbl.create 16)
    (* This table is used to usage of value declarations.  A declaration is
       identified with its name and location.  The callback attached to a
       declaration is called whenever the value is used explicitly
       (lookup_value) or implicitly (inclusion test between signatures,
       cf Includemod.value_descriptions). *)

let type_declarations = sref (fun () -> Hashtbl.create 16)

type constructor_usage = Positive | Pattern | Privatize
type constructor_usages =
    {
     mutable cu_positive: bool;
     mutable cu_pattern: bool;
     mutable cu_privatize: bool;
    }
let add_constructor_usage cu = function
  | Positive ->
    if not cu.cu_positive then (
      on_backtrack (fun () -> cu.cu_positive <- false);
      cu.cu_positive <- true
    )
  | Pattern ->
    if not cu.cu_pattern then (
      on_backtrack (fun () -> cu.cu_pattern <- false);
      cu.cu_pattern <- true
    )
  | Privatize ->
    if not cu.cu_privatize then (
      on_backtrack (fun () -> cu.cu_privatize <- false);
      cu.cu_privatize <- true
    )

let backtracking_add tbl key value =
  on_backtrack (fun () -> Hashtbl.remove tbl key);
  Hashtbl.add tbl key value

let backtracking_set r v =
  let v' = !r in
  on_backtrack (fun () -> r := v');
  r := v

let constructor_usages () =
  {cu_positive = false; cu_pattern = false; cu_privatize = false}

let used_constructors :
    (string * Location.t * string, (constructor_usage -> unit)) Hashtbl.t ref
  = sref (fun () -> Hashtbl.create 16)

let prefixed_sg = sref (fun () -> Hashtbl.create 113)

type error =
  | Illegal_renaming of string * string * string
  | Inconsistent_import of string * string * string
  | Need_recursive_types of string * string
  | Missing_module of Location.t * Path.t * Path.t
  | Illegal_value_name of Location.t * string

exception Error of error

let error err = raise (Error err)

module EnvLazy : sig
  type ('a,'b) t

  val force : ('a -> 'b) -> ('a,'b) t -> 'b
  val create : 'a -> ('a,'b) t
  val get_arg : ('a,'b) t -> 'a option

  type ('a,'b) eval =
      Done of 'b
    | Raise of exn
    | Thunk of 'a

  val is_val : ('a,'b) t -> bool
  val view : ('a,'b) t ->  ('a,'b) eval
end  = struct

  type ('a,'b) t = ('a,'b) eval ref

  and ('a,'b) eval =
      Done of 'b
    | Raise of exn
    | Thunk of 'a

  let force f x =
    match !x with
        Done x -> x
      | Raise e -> raise e
      | Thunk e ->
          try
            let y = f e in
            x := Done y;
            y
          with e ->
            x := Raise e;
            raise e

  let get_arg x =
    match !x with Thunk a -> Some a | _ -> None

  let create x =
    ref (Thunk x)

  let is_val x =
    match !x with Done _ -> true | _ -> false

  let view x = !x
end

type summary =
    Env_empty
  | Env_value of summary * Ident.t * value_description
  | Env_type of summary * Ident.t * type_declaration
  | Env_extension of summary * Ident.t * extension_constructor
  | Env_module of summary * Ident.t * module_declaration
  | Env_modtype of summary * Ident.t * modtype_declaration
  | Env_class of summary * Ident.t * class_declaration
  | Env_cltype of summary * Ident.t * class_type_declaration
  | Env_open of summary * Path.t
  | Env_functor_arg of summary * Ident.t

module EnvTbl =
  struct
    (* A table indexed by identifier, with an extra slot to record usage. *)
    type 'a t = ('a * (unit -> unit)) Ident.tbl

    let empty = Ident.empty
    let nothing = fun () -> ()

    let already_defined wrap s tbl x =
      wrap (try Some (fst (Ident.find_name s tbl), x) with Not_found -> None)

    let add_open slot wrap id x tbl ref_tbl =
      let slot =
        match slot with
        | None -> nothing
        | Some f ->
          (fun () ->
             let s = Ident.name id in
             f s (already_defined wrap s ref_tbl x)
          )
      in
      Ident.add id (x, slot) tbl

    let add id x tbl =
      Ident.add id (x, nothing) tbl

    let find_same_not_using id tbl =
      fst (Ident.find_same id tbl)

    let find_same id tbl =
      let (x, slot) = Ident.find_same id tbl in
      slot ();
      x

    let find_name s tbl =
      let (x, slot) = Ident.find_name s tbl in
      slot ();
      x

    let find_all s tbl =
      Ident.find_all s tbl

    let fold_name f = Ident.fold_name (fun k (d,_) -> f k d)
    let keys tbl = Ident.fold_all (fun k _ accu -> k::accu) tbl []
  end

type type_descriptions =
    constructor_description list * label_description list

let in_signature_flag = 0x01
let implicit_coercion_flag = 0x02

type t = {
  values: (Path.t * value_description) EnvTbl.t;
  constrs: constructor_description EnvTbl.t;
  labels: label_description EnvTbl.t;
  types: (Path.t * (type_declaration * type_descriptions)) EnvTbl.t;
  modules: (Path.t * module_declaration) EnvTbl.t;
  modtypes: (Path.t * modtype_declaration) EnvTbl.t;
  components: (Path.t * module_components) EnvTbl.t;
  classes: (Path.t * class_declaration) EnvTbl.t;
  cltypes: (Path.t * class_type_declaration) EnvTbl.t;
  functor_args: unit Ident.tbl;
  summary: summary;
  local_constraints: bool;
  gadt_instances: (int * TypeSet.t ref) list;
  flags: int;
  short_paths: Short_paths.t option;
  short_paths_additions: short_paths_addition list;
}

and module_components =
  {
    deprecated: string option;
    comps: (t * Subst.t * Path.t * Types.module_type, module_components_repr)
           EnvLazy.t;
  }

and module_components_repr =
    Structure_comps of structure_components
  | Functor_comps of functor_components

and structure_components = {
  mutable comp_values: (string, (value_description * int)) Tbl.t;
  mutable comp_constrs: (string, (constructor_description * int) list) Tbl.t;
  mutable comp_labels: (string, (label_description * int) list) Tbl.t;
  mutable comp_types:
   (string, ((type_declaration * type_descriptions) * int)) Tbl.t;
  mutable comp_modules:
   (string, ((Subst.t * module_declaration,module_declaration) EnvLazy.t * int)) Tbl.t;
  mutable comp_modtypes: (string, (modtype_declaration * int)) Tbl.t;
  mutable comp_components: (string, (module_components * int)) Tbl.t;
  mutable comp_classes: (string, (class_declaration * int)) Tbl.t;
  mutable comp_cltypes: (string, (class_type_declaration * int)) Tbl.t
}

and functor_components = {
  fcomp_param: Ident.t;                 (* Formal parameter *)
  fcomp_arg: module_type option;        (* Argument signature *)
  fcomp_res: module_type;               (* Result signature *)
  fcomp_cache: (Path.t, module_components) Hashtbl.t;  (* For memoization *)
  fcomp_subst_cache: (Path.t, module_type) Hashtbl.t
}

and short_paths_addition =
  | Type of Ident.t * type_declaration
  | Class_type of Ident.t * class_type_declaration
  | Module_type of Ident.t * modtype_declaration
  | Module of Ident.t * module_declaration * module_components
  | Type_open of Ident.t * Path.t
  | Class_type_open of Ident.t * Path.t
  | Module_type_open of Ident.t * Path.t
  | Module_open of Ident.t * Path.t

let same_constr = ref (fun _ _ _ -> assert false)

(* Helper to decide whether to report an identifier shadowing
   by some 'open'. For labels and constructors, we do not report
   if the two elements are from the same re-exported declaration.

   Later, one could also interpret some attributes on value and
   type declarations to silence the shadowing warnings. *)

let check_shadowing env = function
  | `Constructor (Some (c1, c2))
    when not (!same_constr env c1.cstr_res c2.cstr_res) ->
      Some "constructor"
  | `Label (Some (l1, l2))
    when not (!same_constr env l1.lbl_res l2.lbl_res) ->
      Some "label"
  | `Value (Some _) -> Some "value"
  | `Type (Some _) -> Some "type"
  | `Module (Some _) | `Component (Some _) -> Some "module"
  | `Module_type (Some _) -> Some "module type"
  | `Class (Some _) -> Some "class"
  | `Class_type (Some _) -> Some "class type"
  | `Constructor _ | `Label _
  | `Value None | `Type None | `Module None | `Module_type None
  | `Class None | `Class_type None | `Component None ->
      None

let subst_modtype_maker (subst, md) = {md with md_type = Subst.modtype subst md.md_type}

let empty = {
  values = EnvTbl.empty; constrs = EnvTbl.empty;
  labels = EnvTbl.empty; types = EnvTbl.empty;
  modules = EnvTbl.empty; modtypes = EnvTbl.empty;
  components = EnvTbl.empty; classes = EnvTbl.empty;
  cltypes = EnvTbl.empty;
  summary = Env_empty; local_constraints = false; gadt_instances = [];
  flags = 0;
  functor_args = Ident.empty;
  short_paths = None;
  short_paths_additions = [];
 }

let in_signature b env =
  let flags =
    if b then env.flags lor in_signature_flag
    else env.flags land (lnot in_signature_flag)
  in
  {env with flags}

let implicit_coercion env =
  {env with flags = env.flags lor implicit_coercion_flag}

let is_in_signature env = env.flags land in_signature_flag <> 0
let is_implicit_coercion env = env.flags land implicit_coercion_flag <> 0

let diff_keys is_local tbl1 tbl2 =
  let keys2 = EnvTbl.keys tbl2 in
  List.filter
    (fun id ->
      is_local (EnvTbl.find_same_not_using id tbl2) &&
      try ignore (EnvTbl.find_same_not_using id tbl1); false
      with Not_found -> true)
    keys2

let is_ident = function
    Pident _ -> true
  | Pdot _ | Papply _ -> false

let is_local (p, _) = is_ident p

let is_local_ext = function
  | {cstr_tag = Cstr_extension(p, _)} -> is_ident p
  | _ -> false

let diff env1 env2 =
  diff_keys is_local env1.values env2.values @
  diff_keys is_local_ext env1.constrs env2.constrs @
  diff_keys is_local env1.modules env2.modules @
  diff_keys is_local env1.classes env2.classes

(* Forward declarations *)

let components_of_module' =
  ref ((fun ~deprecated env sub path mty -> assert false) :
         deprecated:string option -> t -> Subst.t -> Path.t -> module_type ->
       module_components)
let components_of_module_maker' =
  ref ((fun (env, sub, path, mty) -> assert false) :
          t * Subst.t * Path.t * module_type -> module_components_repr)
let components_of_functor_appl' =
  ref ((fun f env p1 p2 -> assert false) :
          functor_components -> t -> Path.t -> Path.t -> module_components)
let check_modtype_inclusion =
  (* to be filled with Includemod.check_modtype_inclusion *)
  ref ((fun env mty1 path1 mty2 -> assert false) :
          t -> module_type -> Path.t -> module_type -> unit)
let strengthen =
  (* to be filled with Mtype.strengthen *)
  ref ((fun env mty path -> assert false) :
         t -> module_type -> Path.t -> module_type)

let shorten_module_path =
  (* to be filled with Printtyp.shorten_module_path *)
  ref ((fun env path -> assert false) :
         t -> Path.t -> Path.t)

let md md_type =
  {md_type; md_attributes=[]; md_loc=Location.none}

let get_components c =
  EnvLazy.force !components_of_module_maker' c.comps


(* The name of the compilation unit currently compiled.
   "" if outside a compilation unit. *)

let current_unit = srefk ""

(* Persistent structure descriptions *)

type pers_typemap = (Path.t list Path_aux.Map.t
                     * Path.t list Path_aux.Map.t) option

type pers_struct = {
  ps_name: string;
  ps_sig: signature Lazy.t;
  ps_comps: module_components;
  ps_crcs: (string * Digest.t option) list;
  ps_filename: string;
  ps_flags: pers_flags list;
  ps_typemap: pers_typemap ref;
}

let persistent_structures : (string, pers_struct option) Hashtbl.t ref =
  sref (fun () -> Hashtbl.create 17)

(* Consistency between persistent structures *)

let crc_units = sref Consistbl.create

let imported_units = srefk StringSet.empty

let add_import s =
  imported_units := StringSet.add s !imported_units

let imported_opaque_units = srefk StringSet.empty

let add_imported_opaque s =
  imported_opaque_units := StringSet.add s !imported_opaque_units

let clear_imports () =
  Consistbl.clear !crc_units;
  imported_units := StringSet.empty;
  imported_opaque_units := StringSet.empty

let check_consistency ps =
  try
    List.iter
      (fun (name, crco) ->
         match crco with
            None -> ()
          | Some crc ->
              add_import name;
              Consistbl.check !crc_units name crc ps.ps_filename)
      ps.ps_crcs;
  with Consistbl.Inconsistency(name, source, auth) ->
    error (Inconsistent_import(name, auth, source))

(* Short paths basis *)

let short_paths_basis = sref Short_paths.Basis.create

let short_paths_module_components_desc' = ref (fun _ -> assert false)

let register_pers_for_short_paths ps =
  let deps, alias_deps =
    List.fold_left
      (fun (deps, alias_deps) (name, digest) ->
         Short_paths.Basis.add !short_paths_basis name;
         match digest with
         | None -> deps, name :: alias_deps
         | Some _ -> name :: deps, alias_deps)
      ([], []) ps.ps_crcs
  in
  let path = Pident (Ident.create_persistent ps.ps_name) in
  let desc =
    Short_paths.Desc.Module.(Fresh
      (Signature (lazy (!short_paths_module_components_desc' empty path ps.ps_comps))))
  in
  Short_paths.Basis.load !short_paths_basis ps.ps_name deps alias_deps desc

(* Reading persistent structures from .cmi files *)

let save_pers_struct crc ps =
  let modname = ps.ps_name in
  Hashtbl.add !persistent_structures modname (Some ps);
  register_pers_for_short_paths ps;
  List.iter
    (function
        | Rectypes -> ()
        | Deprecated _ -> ()
        | Opaque -> add_imported_opaque modname)
    ps.ps_flags;
  Consistbl.set !crc_units modname crc ps.ps_filename;
  add_import modname

exception Cmi_cache_store of pers_typemap ref * signature lazy_t

let read_pers_struct check modname filename =
  add_import modname;
  let {Cmi_cache. cmi; cmi_cache} = Cmi_cache.read filename in
  let name = cmi.cmi_name in
  let sign = cmi.cmi_sign in
  let crcs = cmi.cmi_crcs in
  let flags = cmi.cmi_flags in
  let deprecated =
    List.fold_left (fun acc -> function Deprecated s -> Some s | _ -> acc) None
      flags
  in
  let comps =
    !components_of_module' ~deprecated empty Subst.identity
      (Pident(Ident.create_persistent name))
      (Mty_signature sign)
  in
  let ps_typemap, ps_sig = match !cmi_cache with
    | Cmi_cache_store (ps_typemap, ps_sig) -> ps_typemap, ps_sig
    | _ ->
      let ps_typemap = ref None in
      let ps_sig = lazy (Subst.signature Subst.identity sign) in
      cmi_cache := Cmi_cache_store (ps_typemap, ps_sig);
      ps_typemap, ps_sig
  in
  let ps = { ps_name = name;
             ps_sig = ps_sig;
             ps_comps = comps;
             ps_crcs = crcs;
             ps_filename = filename;
             ps_flags = flags;
             ps_typemap;
           } in
  if ps.ps_name <> modname then
    error (Illegal_renaming(modname, ps.ps_name, filename));

  List.iter
    (function
        | Rectypes ->
            if not !Clflags.recursive_types then
              error (Need_recursive_types(ps.ps_name, !current_unit))
        | Deprecated _ -> ()
        | Opaque -> add_imported_opaque modname)
    ps.ps_flags;
  if check then check_consistency ps;
  Hashtbl.add !persistent_structures modname (Some ps);
  register_pers_for_short_paths ps;
  ps

let find_pers_struct check name =
  if name = "*predef*" then raise Not_found;
  match Hashtbl.find !persistent_structures name with
  | Some ps -> ps
  | None -> raise Not_found
  | exception Not_found ->
      let filename =
        try
          find_in_path_uncap !load_path (name ^ ".cmi")
        with Not_found ->
          Hashtbl.add !persistent_structures name None;
          raise Not_found
      in
      read_pers_struct check name filename

(* Emits a warning if there is no valid cmi for name *)
let check_pers_struct name =
  try
    ignore (find_pers_struct false name)
  with
  | Not_found ->
      let warn = Warnings.No_cmi_file(name, None) in
        Location.prerr_warning Location.none warn
  | Cmi_format.Error err ->
      let msg = Format.asprintf "%a" Cmi_format.report_error err in
      let warn = Warnings.No_cmi_file(name, Some msg) in
        Location.prerr_warning Location.none warn
  | Error err ->
      let msg =
        match err with
        | Illegal_renaming(name, ps_name, filename) ->
            Format.asprintf
              " %a@ contains the compiled interface for @ \
               %s when %s was expected"
              Location.print_filename filename ps_name name
        | Inconsistent_import _ -> assert false
        | Need_recursive_types(name, _) ->
            Format.sprintf
              "%s uses recursive types"
              name
        | Missing_module _ -> assert false
        | Illegal_value_name _ -> assert false
      in
      let warn = Warnings.No_cmi_file(name, Some msg) in
        Location.prerr_warning Location.none warn

let read_pers_struct modname filename =
  read_pers_struct true modname filename

let find_pers_struct name =
  find_pers_struct true name

let find_signature name =
  Lazy.force (find_pers_struct name).ps_sig

let check_pers_struct name =
  if not (Hashtbl.mem !persistent_structures name) then begin
    (* PR#6843: record the weak dependency ([add_import]) regardless of
       whether the check suceeds, to help make builds more
       deterministic. *)
    add_import name;
    if (Warnings.is_active (Warnings.No_cmi_file("", None))) then
      !add_delayed_check_forward
        (fun () -> check_pers_struct name)
  end

let reset_cache () =
  current_unit := "";
  Hashtbl.clear !persistent_structures;
  clear_imports ();
  short_paths_basis := Short_paths.Basis.create ();
  Hashtbl.clear !value_declarations;
  Hashtbl.clear !type_declarations;
  Hashtbl.clear !used_constructors;
  Hashtbl.clear !prefixed_sg

let reset_cache_toplevel () =
  (* Delete 'missing cmi' entries from the cache. *)
  let l =
    Hashtbl.fold
      (fun name r acc -> if r = None then name :: acc else acc)
      !persistent_structures []
  in
  List.iter (Hashtbl.remove !persistent_structures) l;
  Hashtbl.clear !value_declarations;
  Hashtbl.clear !type_declarations;
  Hashtbl.clear !used_constructors;
  Hashtbl.clear !prefixed_sg


let set_unit_name name =
  current_unit := name

let get_unit_name () =
  !current_unit

(* Lookup by identifier *)

let rec find_module_descr path env =
  match path with
    Pident id ->
      begin try
        let (p, desc) = EnvTbl.find_same id env.components
        in desc
      with Not_found ->
        if Ident.persistent id && not (Ident.name id = !current_unit)
        then (find_pers_struct (Ident.name id)).ps_comps
        else raise Not_found
      end
  | Pdot(p, s, pos) ->
      begin match get_components (find_module_descr p env) with
        Structure_comps c ->
          let (descr, pos) = Tbl.find s c.comp_components in
          descr
      | Functor_comps f ->
         raise Not_found
      end
  | Papply(p1, p2) ->
      begin match get_components (find_module_descr p1 env) with
        Functor_comps f ->
          !components_of_functor_appl' f env p1 p2
      | Structure_comps c ->
          raise Not_found
      end

let find proj1 proj2 path env =
  match path with
    Pident id ->
      let (p, data) = EnvTbl.find_same id (proj1 env)
      in data
  | Pdot(p, s, pos) ->
      begin match get_components (find_module_descr p env) with
        Structure_comps c ->
          let (data, pos) = Tbl.find s (proj2 c) in data
      | Functor_comps f ->
          raise Not_found
      end
  | Papply(p1, p2) ->
      raise Not_found

let find_value =
  find (fun env -> env.values) (fun sc -> sc.comp_values)
and find_type_full =
  find (fun env -> env.types) (fun sc -> sc.comp_types)
and find_modtype =
  find (fun env -> env.modtypes) (fun sc -> sc.comp_modtypes)
and find_class =
  find (fun env -> env.classes) (fun sc -> sc.comp_classes)
and find_cltype =
  find (fun env -> env.cltypes) (fun sc -> sc.comp_cltypes)

let type_of_cstr path = function
  | {cstr_inlined = Some d; _} ->
      (d, ([], List.map snd (Datarepr.labels_of_type path d)))
  | _ ->
      assert false

let find_type_full path env =
  match Path.constructor_typath path with
  | Regular p ->
      find_type_full p env
  | Cstr (ty_path, s) ->
      let (_, (cstrs, _)) =
        try find_type_full ty_path env
        with Not_found -> assert false
      in
      let cstr =
        try List.find (fun cstr -> cstr.cstr_name = s) cstrs
        with Not_found -> assert false
      in
      type_of_cstr path cstr
  | LocalExt id ->
      let cstr =
        try EnvTbl.find_same id env.constrs
        with Not_found -> assert false
      in
      type_of_cstr path cstr
  | Ext (mod_path, s) ->
      let comps =
        try find_module_descr mod_path env
        with Not_found -> assert false
      in
      let comps =
        match get_components comps with
        | Structure_comps c -> c
        | Functor_comps _ -> assert false
      in
      let exts =
        List.filter
          (function ({cstr_tag=Cstr_extension _}, _) -> true | _ -> false)
          (try Tbl.find s comps.comp_constrs
           with Not_found -> assert false)
      in
      match exts with
      | [(cstr, _)] -> type_of_cstr path cstr
      | _ -> assert false

let find_type p env =
  fst (find_type_full p env)
let find_type_descrs p env =
  snd (find_type_full p env)

let find_module ~alias path env =
  match path with
    Pident id ->
      begin try
        let (p, data) = EnvTbl.find_same id env.modules
        in data
      with Not_found ->
        if Ident.persistent id && not (Ident.name id = !current_unit) then
          let ps = find_pers_struct (Ident.name id) in
          md (Mty_signature(Lazy.force ps.ps_sig))
        else raise Not_found
      end
  | Pdot(p, s, pos) ->
      begin match get_components (find_module_descr p env) with
        Structure_comps c ->
          let (data, pos) = Tbl.find s c.comp_modules in
          EnvLazy.force subst_modtype_maker data
      | Functor_comps f ->
          raise Not_found
      end
  | Papply(p1, p2) ->
      let desc1 = find_module_descr p1 env in
      begin match get_components desc1 with
        Functor_comps f ->
          md begin match f.fcomp_res with
          | Mty_alias p as mty-> mty
          | mty ->
              if alias then mty else
              try
                Hashtbl.find f.fcomp_subst_cache p2
              with Not_found ->
                let mty =
                  Subst.modtype
                    (Subst.add_module f.fcomp_param p2 Subst.identity)
                    f.fcomp_res in
                Hashtbl.add f.fcomp_subst_cache p2 mty;
                mty
          end
      | Structure_comps c ->
          raise Not_found
      end

let required_globals = srefk []
let reset_required_globals () = required_globals := []
let get_required_globals () = !required_globals
let add_required_global id =
  if Ident.global id && not !Clflags.transparent_modules
  && not (List.exists (Ident.same id) !required_globals)
  then required_globals := id :: !required_globals

let rec normalize_path lax env path =
  let path =
    match path with
      Pdot(p, s, pos) ->
        Pdot(normalize_path lax env p, s, pos)
    | Papply(p1, p2) ->
        Papply(normalize_path lax env p1, normalize_path true env p2)
    | _ -> path
  in
  try match find_module ~alias:true path env with
    {md_type=Mty_alias path1} ->
      let path' = normalize_path lax env path1 in
      if lax || !Clflags.transparent_modules then path' else
      let id = Path.head path in
      if Ident.global id && not (Ident.same id (Path.head path'))
      then add_required_global id;
      path'
  | _ -> path
  with Not_found when lax
  || (match path with Pident id -> not (Ident.persistent id) | _ -> true) ->
      path

let normalize_path oloc env path =
  try normalize_path (oloc = None) env path
  with Not_found ->
    match oloc with None -> assert false
    | Some loc ->
        raise (Error(Missing_module(loc, path, normalize_path true env path)))

let find_module = find_module ~alias:false

(* Find the manifest type associated to a type when appropriate:
   - the type should be public or should have a private row,
   - the type should have an associated manifest type. *)
let find_type_expansion path env =
  let decl = find_type path env in
  match decl.type_manifest with
  | Some body when decl.type_private = Public
              || decl.type_kind <> Type_abstract
              || Btype.has_constr_row body ->
                  (decl.type_params, body, may_map snd decl.type_newtype_level)
  (* The manifest type of Private abstract data types without
     private row are still considered unknown to the type system.
     Hence, this case is caught by the following clause that also handles
     purely abstract data types without manifest type definition. *)
  | _ ->
      (* another way to expand is to normalize the path itself *)
      let path' = normalize_path None env path in
      if Path.same path path' then raise Not_found else
      (decl.type_params,
       newgenty (Tconstr (path', decl.type_params, ref Mnil)),
       may_map snd decl.type_newtype_level)

(* Find the manifest type information associated to a type, i.e.
   the necessary information for the compiler's type-based optimisations.
   In particular, the manifest type associated to a private abstract type
   is revealed for the sake of compiler's type-based optimisations. *)
let find_type_expansion_opt path env =
  let decl = find_type path env in
  match decl.type_manifest with
  (* The manifest type of Private abstract data types can still get
     an approximation using their manifest type. *)
  | Some body -> (decl.type_params, body, may_map snd decl.type_newtype_level)
  | _ ->
      let path' = normalize_path None env path in
      if Path.same path path' then raise Not_found else
      (decl.type_params,
       newgenty (Tconstr (path', decl.type_params, ref Mnil)),
       may_map snd decl.type_newtype_level)

let find_modtype_expansion path env =
  match (find_modtype path env).mtd_type with
  | None -> raise Not_found
  | Some mty -> mty

let rec is_functor_arg path env =
  match path with
    Pident id ->
      begin try Ident.find_same id env.functor_args; true
      with Not_found -> false
      end
  | Pdot (p, s, _) -> is_functor_arg p env
  | Papply _ -> true

(* Lookup by name *)

exception Recmodule

let report_deprecated ?loc p deprecated =
  match loc, deprecated with
  | Some loc, Some txt ->
      let txt = if txt = "" then "" else "\n" ^ txt in
      Location.prerr_warning loc
        (Warnings.Deprecated (Printf.sprintf "module %s%s"
                                (Path.name p) txt))
  | _ -> ()

let rec lookup_module_descr_aux ?loc lid env =
  match lid with
    Lident s ->
      begin try
        EnvTbl.find_name s env.components
      with Not_found ->
        if s = !current_unit then raise Not_found;
        let ps = find_pers_struct s in
        (Pident(Ident.create_persistent s), ps.ps_comps)
      end
  | Ldot(l, s) ->
      let (p, descr) = lookup_module_descr ?loc l env in
      begin match get_components descr with
        Structure_comps c ->
          let (descr, pos) = Tbl.find s c.comp_components in
          (Pdot(p, s, pos), descr)
      | Functor_comps f ->
          raise Not_found
      end
  | Lapply(l1, l2) ->
      let (p1, desc1) = lookup_module_descr ?loc l1 env in
      let p2 = lookup_module ~load:true ?loc l2 env in
      let {md_type=mty2} = find_module p2 env in
      begin match get_components desc1 with
        Functor_comps f ->
          Misc.may (!check_modtype_inclusion env mty2 p2) f.fcomp_arg;
          (Papply(p1, p2), !components_of_functor_appl' f env p1 p2)
      | Structure_comps c ->
          raise Not_found
      end

and lookup_module_descr ?loc lid env =
  let (p, comps) as res = lookup_module_descr_aux ?loc lid env in
  report_deprecated ?loc p comps.deprecated;
  res

and lookup_module ~load ?loc lid env : Path.t =
  match lid with
    Lident s ->
      begin try
        let (p, {md_type; md_attributes}) = EnvTbl.find_name s env.modules in
        begin match md_type with
        | Mty_ident (Path.Pident id) when Ident.name id = "#recmod#" ->
          (* see #5965 *)
          raise Recmodule
        | _ -> ()
        end;
        report_deprecated ?loc p
          (Builtin_attributes.deprecated_of_attrs md_attributes);
        p
      with Not_found ->
        if s = !current_unit then raise Not_found;
        let p = Pident(Ident.create_persistent s) in
        if !Clflags.transparent_modules && not load then check_pers_struct s
        else begin
          let ps = find_pers_struct s in
          report_deprecated ?loc p ps.ps_comps.deprecated
        end;
        p
      end
  | Ldot(l, s) ->
      let (p, descr) = lookup_module_descr ?loc l env in
      begin match get_components descr with
        Structure_comps c ->
          let (data, pos) = Tbl.find s c.comp_modules in
          let (comps, _) = Tbl.find s c.comp_components in
          let p = Pdot(p, s, pos) in
          report_deprecated ?loc p comps.deprecated;
          p
      | Functor_comps f ->
          raise Not_found
      end
  | Lapply(l1, l2) ->
      let (p1, desc1) = lookup_module_descr ?loc l1 env in
      let p2 = lookup_module ~load:true ?loc l2 env in
      let {md_type=mty2} = find_module p2 env in
      let p = Papply(p1, p2) in
      begin match get_components desc1 with
        Functor_comps f ->
          Misc.may (!check_modtype_inclusion env mty2 p2) f.fcomp_arg;
          p
      | Structure_comps c ->
          raise Not_found
      end

let lookup proj1 proj2 ?loc lid env =
  match lid with
    Lident s ->
      EnvTbl.find_name s (proj1 env)
  | Ldot(l, s) ->
      let (p, desc) = lookup_module_descr ?loc l env in
      begin match get_components desc with
        Structure_comps c ->
          let (data, pos) = Tbl.find s (proj2 c) in
          (Pdot(p, s, pos), data)
      | Functor_comps f ->
          raise Not_found
      end
  | Lapply(l1, l2) ->
      raise Not_found

let lookup_all_simple proj1 proj2 shadow ?loc lid env =
  match lid with
    Lident s ->
      let xl = EnvTbl.find_all s (proj1 env) in
      let rec do_shadow =
        function
        | [] -> []
        | ((x, f) :: xs) ->
            (x, f) ::
              (do_shadow (List.filter (fun (y, g) -> not (shadow x y)) xs))
      in
        do_shadow xl
  | Ldot(l, s) ->
      let (p, desc) = lookup_module_descr ?loc l env in
      begin match get_components desc with
        Structure_comps c ->
          let comps =
            try Tbl.find s (proj2 c) with Not_found -> []
          in
          List.map
            (fun (data, pos) -> (data, (fun () -> ())))
            comps
      | Functor_comps f ->
          raise Not_found
      end
  | Lapply(l1, l2) ->
      raise Not_found

let has_local_constraints env = env.local_constraints

let cstr_shadow cstr1 cstr2 =
  match cstr1.cstr_tag, cstr2.cstr_tag with
  | Cstr_extension _, Cstr_extension _ -> true
  | _ -> false

let lbl_shadow lbl1 lbl2 = false

let lookup_value =
  lookup (fun env -> env.values) (fun sc -> sc.comp_values)
and lookup_all_constructors =
  lookup_all_simple (fun env -> env.constrs) (fun sc -> sc.comp_constrs)
    cstr_shadow
and lookup_all_labels =
  lookup_all_simple (fun env -> env.labels) (fun sc -> sc.comp_labels)
    lbl_shadow
and lookup_type =
  lookup (fun env -> env.types) (fun sc -> sc.comp_types)
and lookup_modtype =
  lookup (fun env -> env.modtypes) (fun sc -> sc.comp_modtypes)
and lookup_class =
  lookup (fun env -> env.classes) (fun sc -> sc.comp_classes)
and lookup_cltype =
  lookup (fun env -> env.cltypes) (fun sc -> sc.comp_cltypes)

let update_value s f env =
  try
    let ((p, vd), slot) = Ident.find_name s env.values in
    match p with
    | Pident id ->
        let vd2 = f vd in
        {env with values = Ident.add id ((p, vd2), slot) env.values;
                  summary = Env_value(env.summary, id, vd2)}
    | _ ->
        env
  with Not_found ->
    env

let mark_value_used env name vd =
  if not (is_implicit_coercion env) then
    try Hashtbl.find !value_declarations (name, vd.val_loc) ()
    with Not_found -> ()

let mark_type_used env name vd =
  if not (is_implicit_coercion env) then
    try Hashtbl.find !type_declarations (name, vd.type_loc) ()
    with Not_found -> ()

let mark_constructor_used usage env name vd constr =
  if not (is_implicit_coercion env) then
    try Hashtbl.find !used_constructors (name, vd.type_loc, constr) usage
    with Not_found -> ()

let mark_extension_used usage env ext name =
  if not (is_implicit_coercion env) then
    let ty_name = Path.last ext.ext_type_path in
    try Hashtbl.find !used_constructors (ty_name, ext.ext_loc, name) usage
    with Not_found -> ()

let set_value_used_callback name vd callback =
  let key = (name, vd.val_loc) in
  try
    let old = Hashtbl.find !value_declarations key in
    Hashtbl.replace !value_declarations key (fun () -> old (); callback ())
      (* this is to support cases like:
               let x = let x = 1 in x in x
         where the two declarations have the same location
         (e.g. resulting from Camlp4 expansion of grammar entries) *)
  with Not_found ->
    Hashtbl.add !value_declarations key callback

let set_type_used_callback name td callback =
  let loc = td.type_loc in
  if loc.Location.loc_ghost then ()
  else let key = (name, loc) in
  let old =
    try Hashtbl.find !type_declarations key
    with Not_found -> assert false
  in
  Hashtbl.replace !type_declarations key (fun () -> callback old)

let lookup_value ?loc lid env =
  let (_, desc) as r = lookup_value ?loc lid env in
  mark_value_used env (Longident.last lid) desc;
  r

let lookup_type ?loc lid env =
  let (path, (decl, _)) = lookup_type ?loc lid env in
  mark_type_used env (Longident.last lid) decl;
  path

let mark_type_path env path =
  try
    let decl = find_type path env in
    mark_type_used env (Path.last path) decl
  with Not_found -> ()

let ty_path t =
  match repr t with
  | {desc=Tconstr(path, _, _)} -> path
  | _ -> assert false

let lookup_constructor ?loc lid env =
  match lookup_all_constructors ?loc lid env with
    [] -> raise Not_found
  | (desc, use) :: _ ->
      mark_type_path env (ty_path desc.cstr_res);
      use ();
      desc

let is_lident = function
    Lident _ -> true
  | _ -> false

let lookup_all_constructors ?loc lid env =
  try
    let cstrs = lookup_all_constructors ?loc lid env in
    let wrap_use desc use () =
      mark_type_path env (ty_path desc.cstr_res);
      use ()
    in
    List.map (fun (cstr, use) -> (cstr, wrap_use cstr use)) cstrs
  with
    Not_found when is_lident lid -> []

let mark_constructor usage env name desc =
  if not (is_implicit_coercion env)
  then match desc.cstr_tag with
  | Cstr_extension _ ->
      begin
        let ty_path = ty_path desc.cstr_res in
        let ty_name = Path.last ty_path in
        try Hashtbl.find !used_constructors (ty_name, desc.cstr_loc, name) usage
        with Not_found -> ()
      end
  | _ ->
      let ty_path = ty_path desc.cstr_res in
      let ty_decl = try find_type ty_path env with Not_found -> assert false in
      let ty_name = Path.last ty_path in
      mark_constructor_used usage env ty_name ty_decl name

let lookup_label ?loc lid env =
  match lookup_all_labels ?loc lid env with
    [] -> raise Not_found
  | (desc, use) :: _ ->
      mark_type_path env (ty_path desc.lbl_res);
      use ();
      desc

let lookup_all_labels ?loc lid env =
  try
    let lbls = lookup_all_labels ?loc lid env in
    let wrap_use desc use () =
      mark_type_path env (ty_path desc.lbl_res);
      use ()
    in
    List.map (fun (lbl, use) -> (lbl, wrap_use lbl use)) lbls
  with
    Not_found when is_lident lid -> []

let lookup_class ?loc lid env =
  let (_, desc) as r = lookup_class ?loc lid env in
  (* special support for Typeclass.unbound_class *)
  if Path.name desc.cty_path = "" then ignore (lookup_type ?loc lid env)
  else mark_type_path env desc.cty_path;
  r

let lookup_cltype ?loc lid env =
  let (_, desc) as r = lookup_cltype ?loc lid env in
  if Path.name desc.clty_path = "" then ignore (lookup_type ?loc lid env)
  else mark_type_path env desc.clty_path;
  mark_type_path env desc.clty_path;
  r

(* Iter on an environment (ignoring the body of functors and
   not yet evaluated structures) *)

let rec iter_env_components only_val env proj path path' {comps = mcomps} ft fma =
  if not only_val || EnvLazy.is_val mcomps then
  match EnvLazy.force !components_of_module_maker' mcomps with
    Structure_comps comps ->
    Tbl.iter
      (fun s (d, n) -> ft (Pdot (path, s, n)) (Pdot (path', s, n), d))
      (proj comps);
    Tbl.iter
      (fun s (c, n) ->
         let is_alias =
           try
             let envl, _ = Tbl.find s comps.comp_modules in
             match EnvLazy.view envl with
             | EnvLazy.Raise _ -> false
             | EnvLazy.Done md | EnvLazy.Thunk (_,md) ->
               match md.md_type with
               | Types.Mty_alias alias ->
                 let path = Pdot (path, s, n) in
                 let alias =
                   let pid = Path.head alias in
                   if Ident.persistent pid
                   && not (Hashtbl.mem !persistent_structures (Ident.name pid))
                   then
                     alias
                   else
                     normalize_path None env path
                 in
                 fma path alias; true
               | _ -> false
           with Not_found -> false
         in
         if not is_alias then
           iter_env_components only_val env proj
             (Pdot (path, s, n)) (Pdot (path', s, n)) c ft fma)
      comps.comp_components
  | Functor_comps _ -> ()

let iter_pers_env only_val proj ft fma name env =
  match
    (try Hashtbl.find !persistent_structures name
     with Not_found -> None)
  with
  | Some ps ->
    let id = Pident (Ident.create_persistent name) in
    iter_env_components only_val env proj id id ps.ps_comps ft fma
  | None -> ()

let iter_env ?(only_val=false) proj1 proj2 ft fma env =
  Ident.iter (fun id (x,_) -> ft (Pident id) x) (proj1 env);
  Ident.iter (fun id ((path, comps), _) ->
      iter_env_components only_val env proj2 (Pident id) path comps ft fma)
    env.components

let iter_types_and_aliases ?only_val f =
  iter_env ?only_val (fun env -> env.types) (fun sc -> sc.comp_types) f

let iter_module_types_and_aliases ?(only_val=false) ft fma ident env =
  if Ident.persistent ident then
    iter_pers_env only_val (fun sc -> sc.comp_types) ft fma (Ident.name ident) env
  else
    Ident.iter (fun id ((path, comps), _) ->
        iter_env_components only_val env (fun sc -> sc.comp_types)
          (Pident id) path comps ft fma)
      env.components

let find_pers_map name =
  match Hashtbl.find !persistent_structures name with
  | Some {ps_typemap = {contents = Some map}} -> map
  | _ -> raise Not_found

let set_pers_map name map =
  match Hashtbl.find !persistent_structures name with
  | Some ps -> ps.ps_typemap := Some map
  | None -> raise Not_found

let same_types env1 env2 =
  env1.types == env2.types && env1.components == env2.components

let used_persistent () =
  let r = ref Concr.empty in
  Hashtbl.iter (fun s pso -> if pso != None then r := Concr.add s !r)
    !persistent_structures;
  !r

let find_all_comps proj s (p,mcomps) =
  match get_components mcomps with
    Functor_comps _ -> []
  | Structure_comps comps ->
      try let (c,n) = Tbl.find s (proj comps) in [Pdot(p,s,n), c]
      with Not_found -> []

let rec find_shadowed_comps path env =
  match path with
    Pident id ->
      List.map fst (Ident.find_all (Ident.name id) env.components)
  | Pdot (p, s, _) ->
      let l = find_shadowed_comps p env in
      let l' =
        List.map (find_all_comps (fun comps -> comps.comp_components) s) l in
      List.flatten l'
  | Papply _ -> []

let find_shadowed proj1 proj2 path env =
  match path with
    Pident id ->
      List.map fst (Ident.find_all (Ident.name id) (proj1 env))
  | Pdot (p, s, _) ->
      let l = find_shadowed_comps p env in
      let l' = List.map (find_all_comps proj2 s) l in
      List.flatten l'
  | Papply _ -> []

let find_shadowed_types path env =
  let l =
    find_shadowed
      (fun env -> env.types) (fun comps -> comps.comp_types) path env
  in
  List.map fst l


(* GADT instance tracking *)

let add_gadt_instance_level lv env =
  {env with
   gadt_instances = (lv, ref TypeSet.empty) :: env.gadt_instances}

let is_Tlink = function {desc = Tlink _} -> true | _ -> false

let gadt_instance_level env t =
  let rec find_instance = function
      [] -> None
    | (lv, r) :: rem ->
        if TypeSet.exists is_Tlink !r then
          (* Should we use set_typeset ? *)
          r := TypeSet.fold (fun ty -> TypeSet.add (repr ty)) !r TypeSet.empty;
        if TypeSet.mem t !r then Some lv else find_instance rem
  in find_instance env.gadt_instances

let add_gadt_instances env lv tl =
  let r =
    try List.assoc lv env.gadt_instances with Not_found -> assert false in
  (* Format.eprintf "Added";
  List.iter (fun ty -> Format.eprintf "@ %a" !Btype.print_raw ty) tl;
  Format.eprintf "@."; *)
  set_typeset r (List.fold_right TypeSet.add tl !r)

(* Only use this after expand_head! *)
let add_gadt_instance_chain env lv t =
  let r =
    try List.assoc lv env.gadt_instances with Not_found -> assert false in
  let rec add_instance t =
    let t = repr t in
    if not (TypeSet.mem t !r) then begin
      (* Format.eprintf "@ %a" !Btype.print_raw t; *)
      set_typeset r (TypeSet.add t !r);
      match t.desc with
        Tconstr (p, _, memo) ->
          may add_instance (find_expans Private p !memo)
      | _ -> ()
    end
  in
  (* Format.eprintf "Added chain"; *)
  add_instance t
  (* Format.eprintf "@." *)

(* Expand manifest module type names at the top of the given module type *)

let rec scrape_alias env ?path mty =
  match mty, path with
    Mty_ident p, _ ->
      begin try
        scrape_alias env (find_modtype_expansion p env) ?path
      with Not_found ->
        mty
      end
  | Mty_alias path, _ ->
      begin try
        scrape_alias env (find_module path env).md_type ~path
      with Not_found ->
        (*Location.prerr_warning Location.none
          (Warnings.No_cmi_file (Path.name path));*)
        mty
      end
  | mty, Some path ->
      !strengthen env mty path
  | _ -> mty

let scrape_alias env mty = scrape_alias env mty

(* Given a signature and a root path, prefix all idents in the signature
   by the root path and build the corresponding substitution. *)

let rec prefix_idents root pos sub = function
    [] -> ([], sub)
  | Sig_value(id, decl) :: rem ->
      let p = Pdot(root, Ident.name id, pos) in
      let nextpos = match decl.val_kind with Val_prim _ -> pos | _ -> pos+1 in
      let (pl, final_sub) = prefix_idents root nextpos sub rem in
      (p::pl, final_sub)
  | Sig_type(id, decl, _) :: rem ->
      let p = Pdot(root, Ident.name id, nopos) in
      let (pl, final_sub) =
        prefix_idents root pos (Subst.add_type id p sub) rem in
      (p::pl, final_sub)
  | Sig_typext(id, ext, _) :: rem ->
      let p = Pdot(root, Ident.name id, pos) in
      (* we extend the substitution in case of an inlined record *)
      let (pl, final_sub) =
        prefix_idents root (pos+1) (Subst.add_type id p sub) rem in
      (p::pl, final_sub)
  | Sig_module(id, mty, _) :: rem ->
      let p = Pdot(root, Ident.name id, pos) in
      let (pl, final_sub) =
        prefix_idents root (pos+1) (Subst.add_module id p sub) rem in
      (p::pl, final_sub)
  | Sig_modtype(id, decl) :: rem ->
      let p = Pdot(root, Ident.name id, nopos) in
      let (pl, final_sub) =
        prefix_idents root pos
                      (Subst.add_modtype id (Mty_ident p) sub) rem in
      (p::pl, final_sub)
  | Sig_class(id, decl, _) :: rem ->
      (* pretend this is a type, cf. PR#6650 *)
      let p = Pdot(root, Ident.name id, pos) in
      let (pl, final_sub) =
        prefix_idents root (pos + 1) (Subst.add_type id p sub) rem in
      (p::pl, final_sub)
  | Sig_class_type(id, decl, _) :: rem ->
      let p = Pdot(root, Ident.name id, nopos) in
      let (pl, final_sub) =
        prefix_idents root pos (Subst.add_type id p sub) rem in
      (p::pl, final_sub)

let set_nongen_level sub path =
  Subst.set_nongen_level sub (Path.binding_time path)

let prefix_idents root sub sg =
  let sub = set_nongen_level sub root in
  if sub = set_nongen_level Subst.identity root then
    let sgs =
      try
        Hashtbl.find !prefixed_sg root
      with Not_found ->
        let sgs = ref [] in
        Hashtbl.add !prefixed_sg root sgs;
        sgs
    in
    try
      List.assq sg !sgs
    with Not_found ->
      let r = prefix_idents root 0 sub sg in
      sgs := (sg, r) :: !sgs;
      r
  else
    prefix_idents root 0 sub sg

(* Short path additions *)

let short_paths_type predef id path decl old =
  if not predef && !Clflags.real_paths then old
  else begin
    let addition =
      match path with
      | Pident id' when Ident.same id id' -> Type(id, decl)
      | _ -> Type_open(id, path)
    in
    addition :: old
  end

let short_paths_class_type id path decl old =
  if !Clflags.real_paths then old
  else begin
    let addition =
      match path with
      | Pident id' when Ident.same id id' -> Class_type(id, decl)
      | _ -> Class_type_open(id, path)
    in
    addition :: old
  end

let short_paths_module_type id path decl old =
  if !Clflags.real_paths then old
  else begin
    let addition =
      match path with
      | Pident id' when Ident.same id id' -> Module_type(id, decl)
      | _ -> Module_type_open(id, path)
    in
    addition :: old
  end

let short_paths_module id path decl comps old =
  if !Clflags.real_paths then old
  else begin
    let addition =
      match path with
      | Pident id' when Ident.same id id' -> Module(id, decl, comps)
      | _ -> Module_open(id, path)
    in
    addition :: old
  end

(* Compute structure descriptions *)

let add_to_tbl id decl tbl =
  let decls =
    try Tbl.find id tbl with Not_found -> [] in
  Tbl.add id (decl :: decls) tbl

let rec components_of_module ~deprecated env sub path mty =
  {
    deprecated;
    comps = EnvLazy.create (env, sub, path, mty)
  }

and components_of_module_maker (env, sub, path, mty) =
  (match scrape_alias env mty with
    Mty_signature sg ->
      let c =
        { comp_values = Tbl.empty;
          comp_constrs = Tbl.empty;
          comp_labels = Tbl.empty; comp_types = Tbl.empty;
          comp_modules = Tbl.empty; comp_modtypes = Tbl.empty;
          comp_components = Tbl.empty; comp_classes = Tbl.empty;
          comp_cltypes = Tbl.empty } in
      let pl, sub = prefix_idents path sub sg in
      let env = ref env in
      let pos = ref 0 in
      List.iter2 (fun item path ->
        match item with
          Sig_value(id, decl) ->
            let decl' = Subst.value_description sub decl in
            c.comp_values <-
              Tbl.add (Ident.name id) (decl', !pos) c.comp_values;
            begin match decl.val_kind with
              Val_prim _ -> () | _ -> incr pos
            end
        | Sig_type(id, decl, _) ->
            let decl' = Subst.type_declaration sub decl in
            let constructors =
              List.map snd (Datarepr.constructors_of_type path decl') in
            let labels =
              List.map snd (Datarepr.labels_of_type path decl') in
            c.comp_types <-
              Tbl.add (Ident.name id)
                ((decl', (constructors, labels)), nopos)
                  c.comp_types;
            List.iter
              (fun descr ->
                c.comp_constrs <-
                  add_to_tbl descr.cstr_name (descr, nopos) c.comp_constrs)
              constructors;
            List.iter
              (fun descr ->
                c.comp_labels <-
                  add_to_tbl descr.lbl_name (descr, nopos) c.comp_labels)
              labels;
            env := store_type_infos id (Pident id) decl !env
        | Sig_typext(id, ext, _) ->
            let ext' = Subst.extension_constructor sub ext in
            let descr = Datarepr.extension_descr path ext' in
            c.comp_constrs <-
              add_to_tbl (Ident.name id) (descr, !pos) c.comp_constrs;
            incr pos
        | Sig_module(id, md, _) ->
            let md' = EnvLazy.create (sub, md) in
            c.comp_modules <-
              Tbl.add (Ident.name id) (md', !pos) c.comp_modules;
            let deprecated =
              Builtin_attributes.deprecated_of_attrs md.md_attributes
            in
            let comps = components_of_module ~deprecated !env sub path md.md_type in
            c.comp_components <-
              Tbl.add (Ident.name id) (comps, !pos) c.comp_components;
            env := store_module id (Pident id) md !env;
            incr pos
        | Sig_modtype(id, decl) ->
            let decl' = Subst.modtype_declaration sub decl in
            c.comp_modtypes <-
              Tbl.add (Ident.name id) (decl', nopos) c.comp_modtypes;
            env := store_modtype id (Pident id) decl !env
        | Sig_class(id, decl, _) ->
            let decl' = Subst.class_declaration sub decl in
            c.comp_classes <-
              Tbl.add (Ident.name id) (decl', !pos) c.comp_classes;
            incr pos
        | Sig_class_type(id, decl, _) ->
            let decl' = Subst.cltype_declaration sub decl in
            c.comp_cltypes <-
              Tbl.add (Ident.name id) (decl', !pos) c.comp_cltypes)
        sg pl;
        Structure_comps c
  | Mty_functor(param, ty_arg, ty_res) ->
        Functor_comps {
          fcomp_param = param;
          (* fcomp_arg and fcomp_res must be prefixed eagerly, because
             they are interpreted in the outer environment *)
          fcomp_arg = may_map (Subst.modtype sub) ty_arg;
          fcomp_res = Subst.modtype (set_nongen_level sub path) ty_res;
          fcomp_cache = Hashtbl.create 17;
          fcomp_subst_cache = Hashtbl.create 17 }
  | Mty_ident _
  | Mty_alias _ ->
        Structure_comps {
          comp_values = Tbl.empty;
          comp_constrs = Tbl.empty;
          comp_labels = Tbl.empty;
          comp_types = Tbl.empty;
          comp_modules = Tbl.empty; comp_modtypes = Tbl.empty;
          comp_components = Tbl.empty; comp_classes = Tbl.empty;
          comp_cltypes = Tbl.empty })

(* Insertion of bindings by identifier + path *)

and check_usage loc id warn tbl =
  if not loc.Location.loc_ghost && Warnings.is_active (warn "") then begin
    let name = Ident.name id in
    let key = (name, loc) in
    if Hashtbl.mem tbl key then ()
    else let used = ref false in
    Hashtbl.add tbl key (fun () -> used := true);
    if not (name = "" || name.[0] = '_' || name.[0] = '#')
    then
      !add_delayed_check_forward
        (fun () -> if not !used then Location.prerr_warning loc (warn name))
  end;

and check_value_name name loc =
  (* Note: we could also check here general validity of the
     identifier, to protect against bad identifiers forged by -pp or
     -ppx preprocessors. *)

  if String.length name > 0 && (name.[0] = '#') then
    for i = 1 to String.length name - 1 do
      if name.[i] = '#' then
        raise (Error(Illegal_value_name(loc, name)))
    done


and store_value ?check id path decl env =
  check_value_name (Ident.name id) decl.val_loc;
  may (fun f -> check_usage decl.val_loc id f !value_declarations) check;
  { env with
    values = EnvTbl.add id (path, decl) env.values;
    summary = Env_value(env.summary, id, decl) }

and store_type ~check ~predef id path info env =
  let loc = info.type_loc in
  if check then
    check_usage loc id (fun s -> Warnings.Unused_type_declaration s)
      !type_declarations;
  let constructors = Datarepr.constructors_of_type path info in
  let labels = Datarepr.labels_of_type path info in
  let descrs = (List.map snd constructors, List.map snd labels) in

  if check && not loc.Location.loc_ghost &&
    Warnings.is_active (Warnings.Unused_constructor ("", false, false))
  then begin
    let ty = Ident.name id in
    List.iter
      begin fun (_, {cstr_name = c; _}) ->
        let k = (ty, loc, c) in
        if not (Hashtbl.mem !used_constructors k) then
          let used = constructor_usages () in
          Hashtbl.add !used_constructors k (add_constructor_usage used);
          if not (ty = "" || ty.[0] = '_')
          then !add_delayed_check_forward
              (fun () ->
                if not (is_in_signature env) && not used.cu_positive then
                  Location.prerr_warning loc
                    (Warnings.Unused_constructor
                       (c, used.cu_pattern, used.cu_privatize)))
      end
      constructors
  end;
  { env with
    constrs =
      List.fold_right
        (fun (id, descr) constrs -> EnvTbl.add id descr constrs)
        constructors
        env.constrs;
    labels =
      List.fold_right
        (fun (id, descr) labels -> EnvTbl.add id descr labels)
        labels
        env.labels;
    types =
      EnvTbl.add id (path, (info, descrs)) env.types;
    summary = Env_type(env.summary, id, info);
    short_paths_additions =
      short_paths_type predef id path info env.short_paths_additions; }

and store_type_infos id path info env =
  (* Simplified version of store_type that doesn't compute and store
     constructor and label infos, but simply record the arity and
     manifest-ness of the type.  Used in components_of_module to
     keep track of type abbreviations (e.g. type t = float) in the
     computation of label representations. *)
  { env with
    types = EnvTbl.add id (path, (info,([],[]))) env.types;
    summary = Env_type(env.summary, id, info);
    short_paths_additions =
      short_paths_type false id path info env.short_paths_additions; }

and store_extension ~check id path ext env =
  let loc = ext.ext_loc in
  if check && not loc.Location.loc_ghost &&
    Warnings.is_active (Warnings.Unused_extension ("", false, false))
  then begin
    let ty = Path.last ext.ext_type_path in
    let n = Ident.name id in
    let k = (ty, loc, n) in
    if not (Hashtbl.mem !used_constructors k) then begin
      let used = constructor_usages () in
      backtracking_add !used_constructors k (add_constructor_usage used);
      !add_delayed_check_forward
        (fun () ->
          if not (is_in_signature env) && not used.cu_positive then
            Location.prerr_warning loc
              (Warnings.Unused_extension
                 (n, used.cu_pattern, used.cu_privatize)
              )
        )
    end;
  end;
  { env with
    constrs = EnvTbl.add id (Datarepr.extension_descr path ext) env.constrs;
    summary = Env_extension(env.summary, id, ext) }

and store_module id path md env =
  let deprecated = Builtin_attributes.deprecated_of_attrs md.md_attributes in
  let comps =
    components_of_module ~deprecated
           env Subst.identity path md.md_type
  in
  { env with
    modules = EnvTbl.add id (path, md) env.modules;
    components = EnvTbl.add id (path, comps) env.components;
    summary = Env_module(env.summary, id, md);
    short_paths_additions =
      short_paths_module id path md comps env.short_paths_additions; }

and store_modtype id path info env =
  { env with
    modtypes = EnvTbl.add id (path, info) env.modtypes;
    summary = Env_modtype(env.summary, id, info);
    short_paths_additions =
      short_paths_module_type id path info env.short_paths_additions; }

and store_class id path desc env =
  { env with
    classes = EnvTbl.add id (path, desc) env.classes;
    summary = Env_class(env.summary, id, desc) }

and store_cltype id path desc env =
  { env with
    cltypes = EnvTbl.add id (path, desc) env.cltypes;
    summary = Env_cltype(env.summary, id, desc);
    short_paths_additions =
      short_paths_class_type id path desc env.short_paths_additions; }

(* Compute the components of a functor application in a path. *)

let components_of_functor_appl f env p1 p2 =
  try
    Hashtbl.find f.fcomp_cache p2
  with Not_found ->
    let p = Papply(p1, p2) in
    let sub = Subst.add_module f.fcomp_param p2 Subst.identity in
    let mty = Subst.modtype sub f.fcomp_res in
    let comps = components_of_module ~deprecated:None (*???*)
        env Subst.identity p mty in
    Hashtbl.add f.fcomp_cache p2 comps;
    comps

(* Define forward functions *)

let _ =
  components_of_module' := components_of_module;
  components_of_functor_appl' := components_of_functor_appl;
  components_of_module_maker' := components_of_module_maker

(* Insertion of bindings by identifier *)

let add_functor_arg id env =
  {env with
   functor_args = Ident.add id () env.functor_args;
   summary = Env_functor_arg (env.summary, id)}

let add_value ?check id desc env =
  store_value ?check id (Pident id) desc env

let add_type ~check ~predef id info env =
  store_type ~check ~predef id (Pident id) info env

and add_extension ~check id ext env =
  store_extension ~check id (Pident id) ext env

and add_module_declaration ?(arg=false) id md env =
  let path =
    (*match md.md_type with
      Mty_alias path -> normalize_path env path
    | _ ->*) Pident id
  in
  let env = store_module id path md env in
  if arg then add_functor_arg id env else env

and add_modtype id info env =
  store_modtype id (Pident id) info env

and add_class id ty env =
  store_class id (Pident id) ty env

and add_cltype id ty env =
  store_cltype id (Pident id) ty env

let add_module ?arg id mty env =
  add_module_declaration ?arg id (md mty) env

let add_local_constraint id info elv env =
  match info with
    {type_manifest = Some ty; type_newtype_level = Some (lv, _)} ->
      (* elv is the expansion level, lv is the definition level *)
      let env =
        add_type ~check:false ~predef:false
          id {info with type_newtype_level = Some (lv, elv)} env in
      { env with local_constraints = true }
  | _ -> assert false

(* Insertion of bindings by name *)

let enter store_fun name data env =
  let id = Ident.create name in (id, store_fun id (Pident id) data env)

let enter_value ?check = enter (store_value ?check)
and enter_type = enter (store_type ~check:true ~predef:false)
and enter_extension = enter (store_extension ~check:true)
and enter_module_declaration ?arg id md env =
  add_module_declaration ?arg id md env
  (* let (id, env) = enter store_module name md env in
  (id, add_functor_arg ?arg id env) *)
and enter_modtype = enter store_modtype
and enter_class = enter store_class
and enter_cltype = enter store_cltype

let enter_module ?arg s mty env =
  let id = Ident.create s in
  (id, enter_module_declaration ?arg id (md mty) env)

(* Insertion of all components of a signature *)

let add_item comp env =
  match comp with
    Sig_value(id, decl)     -> add_value id decl env
  | Sig_type(id, decl, _)   -> add_type ~check:false ~predef:false id decl env
  | Sig_typext(id, ext, _)  -> add_extension ~check:false id ext env
  | Sig_module(id, md, _)   -> add_module_declaration id md env
  | Sig_modtype(id, decl)   -> add_modtype id decl env
  | Sig_class(id, decl, _)  -> add_class id decl env
  | Sig_class_type(id, decl, _) -> add_cltype id decl env

let rec add_signature sg env =
  match sg with
    [] -> env
  | comp :: rem -> add_signature rem (add_item comp env)

(* Open a signature path *)

let add_components slot root env0 comps =
  let add w comps env0 =
    Tbl.fold
      (fun name (c, pos) acc ->
         EnvTbl.add_open slot w (Ident.hide (Ident.create name))
           (Pdot (root, name, pos), c) acc env0)
      comps env0
  in
  let add_list w comps env0 =
    Tbl.fold
      (fun name c_pos acc ->
         List.fold_right
           (fun (c, _) acc ->
              EnvTbl.add_open slot w
                (Ident.hide (Ident.create name)) c acc env0)
           c_pos acc)
      comps env0
  in
  let add_types w comps env0 additions =
     Tbl.fold
      (fun name ((decl, _) as c, pos) (acc, additions) ->
         let id = Ident.hide (Ident.create name) in
         let path = Pdot(root, name, pos) in
         let acc = EnvTbl.add_open slot w id (path, c) acc env0 in
         let additions = short_paths_type false id path decl additions in
         acc, additions)
      comps (env0, additions)
  in
  let add_cltypes w comps env0 additions =
    Tbl.fold
      (fun name (c, pos) (acc, additions) ->
         let id = Ident.hide (Ident.create name) in
         let path = Pdot(root, name, pos) in
         let acc = EnvTbl.add_open slot w id (path, c) acc env0 in
         let additions = short_paths_class_type id path c additions in
         acc, additions)
      comps (env0, additions)
  in
  let add_modtypes w comps env0 additions =
    Tbl.fold
      (fun name (c, pos) (acc, additions) ->
         let id = Ident.hide (Ident.create name) in
         let path = Pdot(root, name, pos) in
         let acc = EnvTbl.add_open slot w id (path, c) acc env0 in
         let additions = short_paths_module_type id path c additions in
         acc, additions)
      comps (env0, additions)
  in
  let add_modules w comps env0 additions components =
    Tbl.fold
      (fun name (c, pos) (acc, additions) ->
         let id = Ident.hide (Ident.create name) in
         let path = Pdot(root, name, pos) in
         let c = EnvLazy.force subst_modtype_maker c in
         let acc = EnvTbl.add_open slot w id (path, c) acc env0 in
         let comps =
           match Tbl.find name components with
           | comps, _ -> comps
           | exception Not_found -> assert false
         in
         let additions = short_paths_module id path c comps additions in
         acc, additions)
      comps (env0, additions)
  in
  let constrs =
    add_list (fun x -> `Constructor x) comps.comp_constrs env0.constrs
  in
  let labels =
    add_list (fun x -> `Label x) comps.comp_labels env0.labels
  in
  let values =
    add (fun x -> `Value x) comps.comp_values env0.values
  in
  let types, short_paths_additions =
    add_types (fun x -> `Type x) comps.comp_types
      env0.types env0.short_paths_additions
  in
  let modtypes, short_paths_additions =
    add_modtypes (fun x -> `Module_type x) comps.comp_modtypes
      env0.modtypes short_paths_additions
  in
  let classes =
    add (fun x -> `Class x) comps.comp_classes env0.classes
  in
  let cltypes, short_path_additions =
    add_cltypes (fun x -> `Class_type x) comps.comp_cltypes
      env0.cltypes short_paths_additions
  in
  let components =
    add (fun x -> `Component x) comps.comp_components env0.components
  in
  let modules, short_paths_additions =
     (* one should avoid this force, by allowing lazy in env as well *)
    add_modules (fun x -> `Module x) comps.comp_modules
      env0.modules short_paths_additions comps.comp_components
  in
  { env0 with
    summary = Env_open(env0.summary, root);
    constrs;
    labels;
    values;
    types;
    modtypes;
    classes;
    cltypes;
    components;
    modules;
    short_paths_additions;
  }

let open_signature slot root env0 =
  match get_components (find_module_descr root env0) with
  | Functor_comps _ -> None
  | Structure_comps comps -> Some (add_components slot root env0 comps)

(* Open a signature from a file *)

let open_pers_signature name env =
  match open_signature None (Pident(Ident.create_persistent name)) env with
  | Some env -> env
  | None -> assert false (* a compilation unit cannot refer to a functor *)

let open_signature ?(loc = Location.none) ?(toplevel = false) ovf root env =
  if not toplevel && ovf = Asttypes.Fresh && not loc.Location.loc_ghost
     && (Warnings.is_active (Warnings.Unused_open "")
         || Warnings.is_active (Warnings.Open_shadow_identifier ("", ""))
         || Warnings.is_active (Warnings.Open_shadow_label_constructor ("","")))
  then begin
    let used = ref false in
    !add_delayed_check_forward
      (fun () ->
        if not !used then begin
          let root = !shorten_module_path env root in
          Location.prerr_warning loc (Warnings.Unused_open (Path.name root))
        end
      );
    let shadowed = ref [] in
    let slot s b =
      begin match check_shadowing env b with
      | Some kind when not (List.mem (kind, s) !shadowed) ->
          shadowed := (kind, s) :: !shadowed;
          let w =
            match kind with
            | "label" | "constructor" ->
                Warnings.Open_shadow_label_constructor (kind, s)
            | _ -> Warnings.Open_shadow_identifier (kind, s)
          in
          Location.prerr_warning loc w
      | _ -> ()
      end;
      backtracking_set used true
    in
    open_signature (Some slot) root env
  end
  else open_signature None root env

(* Read a signature from a file *)

let read_signature modname filename =
  let ps = read_pers_struct modname filename in
  Lazy.force ps.ps_sig

(* Return the CRC of the interface of the given compilation unit *)

let crc_of_unit name =
  let ps = find_pers_struct name in
  let crco =
    try
      List.assoc name ps.ps_crcs
    with Not_found ->
      assert false
  in
    match crco with
      None -> assert false
    | Some crc -> crc

(* Return the list of imported interfaces with their CRCs *)

let imports () =
  Consistbl.extract (StringSet.elements !imported_units) !crc_units

(* Returns true if [s] is an opaque imported module  *)
let is_imported_opaque s =
  StringSet.mem s !imported_opaque_units

(* Save a signature to a file *)

let save_signature_with_imports ~deprecated sg modname filename imports =
  (*prerr_endline filename;
  List.iter (fun (name, crc) -> prerr_endline name) imports;*)
  Btype.cleanup_abbrev ();
  Subst.reset_for_saving ();
  let sg = Subst.signature (Subst.for_saving Subst.identity) sg in
  let flags =
    List.concat [
      if !Clflags.recursive_types then [Cmi_format.Rectypes] else [];
      if !Clflags.opaque then [Cmi_format.Opaque] else [];
      (match deprecated with Some s -> [Deprecated s] | None -> []);
    ]
  in
  let oc = open_out_bin filename in
  try
    let cmi = {
      cmi_name = modname;
      cmi_sign = sg;
      cmi_crcs = imports;
      cmi_flags = flags;
    } in
    let crc = output_cmi filename oc cmi in
    close_out oc;
    (* Enter signature in persistent table so that imported_unit()
       will also return its crc *)
    let comps =
      components_of_module ~deprecated
        empty Subst.identity
        (Pident(Ident.create_persistent modname)) (Mty_signature sg) in
    let ps =
      { ps_name = modname;
        ps_sig = lazy (Subst.signature Subst.identity sg);
        ps_comps = comps;
        ps_crcs = (cmi.cmi_name, Some crc) :: imports;
        ps_filename = filename;
        ps_flags = cmi.cmi_flags;
        ps_typemap = ref None;
      } in
    save_pers_struct crc ps;
    sg
  with exn ->
    close_out oc;
    remove_file filename;
    raise exn

let save_signature ~deprecated sg modname filename =
  save_signature_with_imports ~deprecated sg modname filename (imports())

(* Folding on environments *)

let find_all proj1 proj2 f lid env acc =
  match lid with
    | None ->
      EnvTbl.fold_name
        (fun id (p, data) acc -> f (Ident.name id) p data acc)
        (proj1 env) acc
    | Some l ->
      let p, desc = lookup_module_descr l env in
      begin match get_components desc with
          Structure_comps c ->
            Tbl.fold
              (fun s (data, pos) acc -> f s (Pdot (p, s, pos)) data acc)
              (proj2 c) acc
        | Functor_comps _ ->
            acc
      end

let find_all_simple_list proj1 proj2 f lid env acc =
  match lid with
    | None ->
      EnvTbl.fold_name
        (fun id data acc -> f data acc)
        (proj1 env) acc
    | Some l ->
      let p, desc = lookup_module_descr l env in
      begin match get_components desc with
          Structure_comps c ->
            Tbl.fold
              (fun s comps acc ->
                match comps with
                  [] -> acc
                | (data, pos) :: _ ->
                  f data acc)
              (proj2 c) acc
        | Functor_comps _ ->
            acc
      end

let fold_modules f lid env acc =
  match lid with
    | None ->
      let acc =
        EnvTbl.fold_name
          (fun id (p, data) acc -> f (Ident.name id) p data acc)
          env.modules
          acc
      in
      Hashtbl.fold
        (fun name ps acc ->
          match ps with
              None -> acc
            | Some ps ->
              f name (Pident(Ident.create_persistent name))
                     (md (Mty_signature (Lazy.force ps.ps_sig))) acc)
        !persistent_structures
        acc
    | Some l ->
      let p, desc = lookup_module_descr l env in
      begin match get_components desc with
          Structure_comps c ->
            Tbl.fold
              (fun s (data, pos) acc ->
                f s (Pdot (p, s, pos))
                    (EnvLazy.force subst_modtype_maker data) acc)
              c.comp_modules
              acc
        | Functor_comps _ ->
            acc
      end

let fold_values f =
  find_all (fun env -> env.values) (fun sc -> sc.comp_values) f
and fold_constructors f =
  find_all_simple_list (fun env -> env.constrs) (fun sc -> sc.comp_constrs) f
and fold_labels f =
  find_all_simple_list (fun env -> env.labels) (fun sc -> sc.comp_labels) f
and fold_types f =
  find_all (fun env -> env.types) (fun sc -> sc.comp_types) f
and fold_modtypes f =
  find_all (fun env -> env.modtypes) (fun sc -> sc.comp_modtypes) f
and fold_classs f =
  find_all (fun env -> env.classes) (fun sc -> sc.comp_classes) f
and fold_cltypes f =
  find_all (fun env -> env.cltypes) (fun sc -> sc.comp_cltypes) f


(* Update short paths *)

let rec index l x =
  match l with
    [] -> raise Not_found
  | a :: l -> if x == a then 0 else 1 + index l x

let rec uniq = function
    [] -> true
  | a :: l -> not (List.memq a l) && uniq l

let short_paths_type_desc decl =
  let open Short_paths.Desc.Type in
  match decl.type_manifest with
  | None -> Fresh
  | Some ty ->
    let ty = repr ty in
    if ty.level <> generic_level then Fresh
    else begin
      match decl.type_private, decl.type_kind with
      | Private, Type_abstract -> Fresh
      | _, _ -> begin
        let params = List.map repr decl.type_params in
        match ty with
        | {desc = Tconstr (path, args, _)} ->
            let args = List.map repr args in
            if List.length params = List.length args
               && List.for_all2 (==) params args
            then Alias path
            else if List.length params <= List.length args
                    || not (uniq args) then Fresh
            else begin
              match List.map (index params) args with
              | exception Not_found -> Fresh
              | ns -> Subst(path, ns)
            end
        | ty -> begin
            match index params ty with
            | exception Not_found -> Fresh
            | n -> Nth n
          end
      end
    end

let short_paths_class_type_desc clty =
  let open Short_paths.Desc.Class_type in
  match clty.clty_type with
  | Cty_signature _ | Cty_arrow _ -> Fresh
  | Cty_constr(path, args, _) ->
      let params = List.map repr clty.clty_params in
      let args = List.map repr args in
      if List.length params = List.length args
      && List.for_all2 (==) params args
      then Alias path
      else if List.length params <= List.length args
             || not (uniq args) then Fresh
      else begin
        match List.map (index params) args with
        | exception Not_found -> Fresh
        | ns -> Subst(path, ns)
      end

let short_paths_module_type_desc mty =
  let open Short_paths.Desc.Module_type in
  match mty with
  | None -> Fresh
  | Some (Mty_ident path) -> Alias path
  | Some (Mty_signature _ | Mty_functor _) -> Fresh
  | Some (Mty_alias _) -> assert false

let rec short_paths_module_desc env mpath mty comp =
  let open Short_paths.Desc.Module in
  match mty with
  | Mty_alias path -> Alias path
  | Mty_ident path -> begin
      match find_modtype_expansion path env with
      | exception Not_found -> Fresh (Signature (lazy []))
      | mty -> short_paths_module_desc env mpath mty comp
    end
  | Mty_signature _ ->
      let components =
        lazy (short_paths_module_components_desc env mpath comp)
      in
      Fresh (Signature components)
  | Mty_functor _ ->
      let apply path =
        short_paths_functor_components_desc env mpath comp path
      in
      Fresh (Functor apply)

and short_paths_module_components_desc env mpath comp =
  match get_components comp with
  | Functor_comps _ -> assert false
  | Structure_comps c ->
      let comps =
        Tbl.fold
          (fun name ((decl, _), _) acc ->
             let desc = short_paths_type_desc decl in
             let item = Short_paths.Desc.Module.Type(name, desc) in
             item :: acc)
          c.comp_types []
      in
       let comps =
         Tbl.fold
          (fun name (clty, _) acc ->
             let desc = short_paths_class_type_desc clty in
             let item = Short_paths.Desc.Module.Class_type(name, desc) in
             item :: acc)
          c.comp_cltypes comps
      in
      let comps =
        Tbl.fold
          (fun name (mtd, _) acc ->
             let desc = short_paths_module_type_desc mtd.mtd_type in
             let item = Short_paths.Desc.Module.Module_type(name, desc) in
             item :: acc)
          c.comp_modtypes comps
      in
      let comps =
        Tbl.fold
          (fun name (data, _) acc ->
             let comps =
               match Tbl.find name c.comp_components with
               | exception Not_found -> assert false
               | comps, _ -> comps
             in
             let mty = EnvLazy.force subst_modtype_maker data in
             let mpath = Pdot(mpath, name, 0) in
             let desc = short_paths_module_desc env mpath mty.md_type comps in
             let item = Short_paths.Desc.Module.Module(name, desc) in
             item :: acc)
          c.comp_modules comps
      in
      comps

and short_paths_functor_components_desc env mpath comp path =
  match get_components comp with
  | Structure_comps _ -> assert false
  | Functor_comps f ->
      let mty =
        try
          Hashtbl.find f.fcomp_subst_cache path
        with Not_found ->
          let mty =
            Subst.modtype
              (Subst.add_module f.fcomp_param path Subst.identity)
              f.fcomp_res
          in
          Hashtbl.add f.fcomp_subst_cache path mty;
          mty
      in
      let comps = components_of_functor_appl f env mpath path in
      let mpath = Papply(mpath, path) in
      short_paths_module_desc env mpath mty comps

let short_paths_additions_desc env additions =
  List.map
    (function
      | Type(id, decl) ->
          let desc = short_paths_type_desc decl in
          Short_paths.Desc.Type(id, desc, true)
      | Class_type(id, clty) ->
          let desc = short_paths_class_type_desc clty in
          Short_paths.Desc.Class_type(id, desc, true)
      | Module_type(id, mtd) ->
          let desc = short_paths_module_type_desc mtd.mtd_type in
          Short_paths.Desc.Module_type(id, desc, true)
      | Module(id, md, comps) ->
          let desc = short_paths_module_desc env (Pident id) md.md_type comps in
          Short_paths.Desc.Module(id, desc, true)
      | Type_open(id, path) ->
          let id = Ident.rename id in
          let desc = Short_paths.Desc.Type.Alias path in
          Short_paths.Desc.Type(id, desc, false)
      | Class_type_open(id, path) ->
          let id = Ident.rename id in
          let desc = Short_paths.Desc.Class_type.Alias path in
          Short_paths.Desc.Class_type(id, desc, false)
      | Module_type_open(id, path) ->
          let id = Ident.rename id in
          let desc = Short_paths.Desc.Module_type.Alias path in
          Short_paths.Desc.Module_type(id, desc, false)
      | Module_open(id, path) ->
          let id = Ident.rename id in
          let desc = Short_paths.Desc.Module.Alias path in
          Short_paths.Desc.Module(id, desc, false))
    additions

let () =
  short_paths_module_components_desc' := short_paths_module_components_desc

let update_short_paths env =
  let env, short_paths =
    match env.short_paths with
    | None ->
      let short_paths = Short_paths.initial !short_paths_basis in
      let env = { env with short_paths = Some short_paths } in
      env, short_paths
    | Some short_paths -> env, short_paths
  in
  match env.short_paths_additions with
  | [] -> env
  | _ :: _ as additions ->
    let short_paths =
      Short_paths.add short_paths
        (lazy (short_paths_additions_desc env additions))
    in
    { env with short_paths = Some short_paths;
               short_paths_additions = []; }

let short_paths env =
  match env.short_paths with
  | None -> Short_paths.initial !short_paths_basis
  | Some short_paths -> short_paths

(* Make the initial environment *)
let (initial_safe_string, initial_unsafe_string) =
  Predef.build_initial_env
    (add_type ~check:false ~predef:true)
    (add_extension ~check:false)
    empty

let add_type ~check id info env =
  add_type ~check ~predef:false id info env

(* Return the environment summary *)

let summary env = env.summary

let last_env = ref empty
let last_reduced_env = ref empty

let keep_only_summary env =
  if !last_env == env then !last_reduced_env
  else begin
    let new_env =
      {
       empty with
       summary = env.summary;
       local_constraints = env.local_constraints;
       flags = env.flags;
      }
    in
    last_env := env;
    last_reduced_env := new_env;
    new_env
  end


let env_of_only_summary env_from_summary env =
  let new_env = env_from_summary env.summary Subst.identity in
  { new_env with
    local_constraints = env.local_constraints;
    flags = env.flags;
  }

(* Error report *)

open Format

let report_error ppf = function
  | Illegal_renaming(modname, ps_name, filename) -> fprintf ppf
      "Wrong file naming: %a@ contains the compiled interface for @ \
       %s when %s was expected"
      Location.print_filename filename ps_name modname
  | Inconsistent_import(name, source1, source2) -> fprintf ppf
      "@[<hov>The files %a@ and %a@ \
              make inconsistent assumptions@ over interface %s@]"
      Location.print_filename source1 Location.print_filename source2 name
  | Need_recursive_types(import, export) ->
      fprintf ppf
        "@[<hov>Unit %s imports from %s, which uses recursive types.@ %s@]"
        export import "The compilation flag -rectypes is required"
  | Missing_module(_, path1, path2) ->
      fprintf ppf "@[@[<hov>";
      if Path.same path1 path2 then
        fprintf ppf "Internal path@ %s@ is dangling." (Path.name path1)
      else
        fprintf ppf "Internal path@ %s@ expands to@ %s@ which is dangling."
          (Path.name path1) (Path.name path2);
      fprintf ppf "@]@ @[%s@ %s@ %s.@]@]"
        "The compiled interface for module" (Ident.name (Path.head path2))
        "was not found"
  | Illegal_value_name(_loc, name) ->
      fprintf ppf "'%s' is not a valid value identifier."
        name

let () =
  Location.register_error_of_exn
    (function
      | Error (Missing_module (loc, _, _)
              | Illegal_value_name (loc, _)
               as err) when loc <> Location.none ->
          Some (Location.error_of_printer ~source:Location.Env loc report_error err)
      | Error err -> Some (Location.error_of_printer_file ~source:Location.Env report_error err)
      | _ -> None
    )

let check_state_consistency () =
  let total = ref 0.0 in
  let result =
    try
      Hashtbl.iter (fun name ps ->
          let time = Sys.time () in
          let filename =
            try Some (find_in_path_uncap !load_path (name ^ ".cmi"))
            with Not_found -> None
          in
          total := !total +. (Sys.time () -. time);
          let invalid =
            match filename, ps with
            | None, None -> false
            | Some filename, Some ps ->
              begin match !(Cmi_cache.(read filename).Cmi_cache.cmi_cache) with
                | Cmi_cache_store (_, ps_sig) ->
                  not (Std.lazy_eq ps_sig ps.ps_sig)
                | _ -> true
              end
            | _, _ -> true
          in
          if invalid then raise Not_found
        ) !persistent_structures;
      true
    with Not_found -> false
  in
  Logger.logf "Env" "check_state_consistency"
    "spent %4.02fms in find_in_path_uncap"  !total;
  result

let with_cmis f = f ()
